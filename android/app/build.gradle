plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id 'com.google.firebase.firebase-perf'  // Google Services plugin
}

// Cargar propiedades del keystore
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.masiglesia.demo"
    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        // Habilitar desugaring para características de Java 8
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    // Configuraciones de firma
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        // Application ID para Amor em Movimento
        applicationId "com.igrejamoremovimento.igreja"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 36
        versionCode 15
         versionName "2.3.4"
        
        // Configuraciones de seguridad
        multiDexEnabled true
        
        // Soporte para páginas de memoria de 16 kB (requerido por Google Play desde nov 2025)
        manifestPlaceholders += [
            'supportsLargeHeap': 'true'
        ]
        
        // Configuraciones de performance
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
        }
    }

    buildTypes {
        release {
            // Configuración para release - MINIFICACIÓN TEMPORALMENTE DESACTIVADA
            minifyEnabled false
            shrinkResources false
            // proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'consumer-proguard-rules.pro', 'proguard-apis-no-sdk.pro'
            
            // Usar configuración de firma para producción
            signingConfig signingConfigs.release
            
            // Configuración específica para APIs No-SDK
            manifestPlaceholders = [
                allowBackup: false,
                supportsRtl: true
            ]
            
            // Optimizaciones adicionales para eliminar APIs No-SDK
            buildConfigField "boolean", "ENABLE_ACCESSIBILITY_FEATURES", "false"
            
            // Configuración agresiva de ProGuard
            // consumerProguardFiles 'proguard-apis-no-sdk.pro'
        }
        debug {
            // Configuración para debug
            minifyEnabled false
            debuggable true
        }
    }
    
    // Habilitar características de build
    buildFeatures {
        buildConfig true
    }
    
    // Configuración de lint para producción
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    
    // AndroidX Window con versión más estable
    implementation 'androidx.window:window:1.4.0'
    
    // Agregar dependencia para desugaring (requerido por flutter_local_notifications)
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
    
    // Import the Firebase BoM
    implementation(platform("com.google.firebase:firebase-bom:33.4.0"))
    // Add the dependencies for Firebase products you want to use
    implementation("com.google.firebase:firebase-analytics")
    implementation("com.google.firebase:firebase-auth")
    implementation("com.google.firebase:firebase-firestore")
    implementation("com.google.firebase:firebase-storage")
    implementation("com.google.firebase:firebase-messaging")
}

// Workaround (Windows): sometimes Flutter's asset bundling tries to copy assets into an
// existing `flutter_assets` output and fails with PathExistsException (errno=183).
// Ensure the folder is recreated fresh for each build variant.
afterEvaluate {
    tasks.matching { it.name.startsWith("compileFlutterBuild") }.configureEach { t ->
        t.doFirst {
            def variantDir = (t.name - "compileFlutterBuild").toLowerCase()
            def flutterAssetsDir = new File(rootDir.parentFile, "build/app/intermediates/flutter/${variantDir}/flutter_assets")
            delete(flutterAssetsDir)
        }
    }
}
